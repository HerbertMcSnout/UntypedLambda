-- Scott-encoded datatypes

{- UNIT -}
unit = \x. x

{- ERROR -}
error = \_. error

{- BOOL -}
true = \t. \f. t
false = \t. \f. f
then = unit -- dummy definitions
else = unit -- ^^^^^
if b then t else f = b t f -- make 'if' actually take 5 args, including 'then' and 'else'
and a b = if a then b else false
or a b = if a then true else b
not a = if a then false else true

{- PAIR -}
pair a b = \f. f a b
fst p = p (\a. \b. a)
snd p = p (\a. \b. b)

{- MAYBE -}
none = \s. \n. n
some a = \s. \n. s a

{- NAT -}
zero = \s. \z. z
succ n = \s. \z. s n

pred n = n (\n'. n') zero

iszero n = n (\_. false) true

add m n = n (add (succ m)) m
sub m n = n (sub (pred m)) m
mul m n = n (\n'. add m (mul m n')) 0
exp m n = n (\n'. mul m (exp m n')) 1

even n = n odd true
odd n = n even false

equal m n =
  if (iszero m) then
    (iszero n)
  else
    (and (not (iszero n)) (equal (pred m) (pred n)))


0 = zero
1 = succ 0
2 = succ 1
3 = succ 2
4 = succ 3
5 = succ 4
6 = succ 5
7 = succ 6
8 = succ 7
9 = succ 8
10 = succ 9
100 = mul 10 10
1000 = mul 10 100
inf = succ inf

{- LIST -}
nil = \c. \n. n
cons h t = \c. \n. c h t
head xs = xs (\h t. h) error
tail xs = xs (\h t. t) nil
singleton h = cons h nil
append xs ys = xs (\h t. cons h t (append t ys)) ys
-- pushes a new element to the back of a list
snoc as a = append as (singleton a)
reverseh acc xs = xs (\x xs. reverseh (cons x acc) xs) acc
reverse xs = reverseh nil xs
map f xs = xs (\x xs. cons (f x) (map f xs)) nil
foldr f n xs = xs (\x xs. f x (foldr f n xs)) n
length xs = xs (\_ xs. succ (length xs)) zero
repeat n x = n (\n. cons x (repeat n x)) nil
cycle xs = append xs (cycle xs)
take n xs = n (\n. xs (\x xs. cons x (take n xs)) nil) nil
drop n xs = n (\n. drop n (tail xs)) xs
